command_line ::= list command_line_endopt ;

command_line_endopt ::= ";"
                     | "&"
                     | ε
                     ;

list ::= or_list ;

or_list ::= and_list or_list_tail ;

or_list_tail ::= "||" and_list or_list_tail
               | ε
               ;

and_list ::= pipeline and_list_tail ;

and_list_tail ::= "&&" pipeline and_list_tail
                | ε
                ;

//  PIPELINE
pipeline ::= core pipeline_tail ;

//      TAIL
pipeline_tail ::= "|" core pipeline_tail
                | ε
                ;
core ::= command
       | grouping
       ;

//  COMMAND
command ::= assignments_list cmd_items ;

cmd_items ::= cmd_item cmd_items
            | ε
            ;
cmd_item ::= word
           | redirection
           ;

// GROUPING (subshell)
grouping ::= "(" list ")" grouping_redirs ;

grouping_redirs ::= redirection grouping_redirs
                      | ε
                      ;

// Redirections (add IO_NUMBER variants?)
redirection ::= redirection_op WORD
              ;

redirection_op ::= "<"
                 | ">"
                 | ">>"
                 | "<<"
                 ;

assignments_list ::= assignment_word assignments_list
                   | ε
                   ;

assignment_word ::= ASSIGNMENT_WORD
                  | APPEND_WORD
                  ;

word ::= WORD
       | ASSIGNMENT_WORD
       | APPEND_WORD
       ;
